; Scythe's AutoMiner Script
; 2024
;
; When I was eleven, I watched my dad attempt to create an autominer script
; in EasyUO back in 2001. I thought it was magic, and I wanted to learn how
; to do it myself. I did not at the time, but I kept that passion for coding
; and finally got a chance in college to take a programming course. I have since
; gotten my masters in software engineering, and I am currently a senior software
; developer and team lead. This project is me coming full circle and realizing
; my first project that I ever wanted to complete. If you are reading this, I
; hope that you get the chance like I have to attain your dreams.
;
; Note: I have done this many times before in other client assistants. I am doing
; this in a specific way that I saw my dad attempt as far as I can remember what
; he was doing. This means a few things:
;    - This is *not* going to be the most efficient thing. It is not a recall
;      miner script where you can get the best color ore as quickly as possible.
;    - It is going to need to use the Delucia area because that's where he was.
;    - I am also going to use pack animals and walking. This is going to greatly
;      increase the things that can go wrong as you have to worry about pathing
;      and also the happiness of the pack animals.


event sysMessage Starting the autominer script!

set %packanimal_id_1 WPSALMD
set %packanimal_id_2 VTZBLMD
set %packanimal_id_3 BUZBLMD
set %packanimal_id_4 WTZBLMD
set %packanimal_id_5 CUZBLMD

set %bank_serial YPJRJMD
set %bank_box C_ , %bank_serial
set %backpack C_ , #BACKPACKID

; Note, small pile is first. Important for combining ore later.
set %ore_pile_graphics |
  +TVJ|
  +GWJ|
  +EWJ|
  +DWJ|

str Count %ore_pile_graphics |
set %ore_pile_graphics_len #strRes - 1

set %locations |
  +_5279_3968_38_|
  +_5280_3968_38_|
  +_5288_3969_38_|
  +_5294_3972_W_|
  +_5299_3966_W_|
  +_5292_3966_42_|
  +_5289_3958_38_|
  +_5283_3963_38_|
  +_5283_3957_38_|
  +_5272_3961_38_|
  +_5273_3955_38_|
  +_5271_3955_38_|
  +_5264_3960_38_|
  +_5263_3955_38_|
  +_5255_3955_38_|
  +_5249_3961_38_|
  +_5239_3956_38_|
  +_5241_3956_38_|
  +_5251_3947_38_|
  +_5242_3947_38_|
  +_5244_3942_38_|
  +_5249_3938_W_|
  +_5246_3935_38_|
  +_5253_3924_38_|
  +_5247_3925_38_|
  +_5245_3921_W_|
  +_5231_3929_38_|
  +_5219_3938_38_|
  +_5204_3927_38_|
  +_5203_3930_38_|
  +_5202_3937_38_|
  +_5199_3942_38_|
  +_5210_3941_W_|
  +_5209_3953_W_|
  +_5197_3954_38_|
  +_5197_3961_38_|
  +_5208_3962_W_|
  +_5217_3969_W_|
  +_5226_3976_W_|
  +_5238_3983_W_|
  +_5250_3990_W_|
  +_5263_3990_W_|
  +_5274_3990_W_|
  +_5283_3989_W_|
  +_5278_3978_W_|

str Count %locations |
set %locations_len #strRes - 1

set %ore_colors |
  +0|
  +2419|
  +2406|
  +2413|
  +2418|
  +2213|
  +2425|
  +2207|
  +2219|

str Count %ore_colors |
set %ore_colors_len #strRes - 1

; %1: X Coordinate Offset
; %2: Y Coordinate Offset
sub clickGumpXY
  set %click_pos_x #CONTPOSX + %1
  set %click_pos_y #CONTPOSY + %2
  click %click_pos_x %click_pos_y
return

; %1: Object ID
; %2: Target Kind
sub useObject
  event sysMessage Using object %1
  set #LOBJECTID %1
  set #LTARGETID %1

  event Macro 17
return

; %1 : X Coordinate
; %2 : Y Coordinate
; %3 : Z Coordinate
sub targetGroundXYZ
  event sysMessage Targeting: %1 %2 %3
  set #LTARGETX %1
  set #LTARGETY %2
  set #LTARGETZ %3
  set #LTARGETKIND 2

  event Macro 22
return

; %1: Object ID
sub targetObject
  set #LTARGETKIND 1
  set #LTARGETID %1
  set #LOBJECTID %1

  event Macro 22
return

sub ensureAtStartingPosition
  ; We must ensure that our character is located in an area where they will not
  ; get stuck on any surrounding buildings.
  ; The area that we are checking for is the rectangle on the east-side of the
  ; delucia bank between the bank and the hut.
  event sysMessage Ensuring character is at starting position

  set %xStartLowerBound 5278
  set %yStartLowerBound 3971
  set %xStartUpperBound 5280
  set %yStartUpperBound 3987

  if ( #CHARPOSX > %xStartUpperBound ||
     + #CHARPOSX < %xStartLowerBound ||
     + #CHARPOSY > %yStartUpperBound ||
     + #CHARPOSY < %yStartLowerBound )
  {
    event sysMessage Please move your character to the east side of the delucia bank and restart.
    stop
  }

  event sysMessage Character was within the starting bounds. Ready to start!
return

sub checkForPackAnimals
  ; Asks the user to select their pack animals so that we know
  ; to hold their state and check their packs.

  ; if we do not have any followers, do not even have to ask.
  if #followers = 0
  {
    event sysMessage You did not have any followers, skipping pack animal check.
  }
return

; Gets the substring between separators
; %1: Search string
; %2: Index
; %3; Seperator (defaults to |)
sub getIndex
{
  ; TODO: Figure out how I want to handle default variables
  ;   and make it so that the sep can be defaulted.

  if %3 <> N/A
  {
    set %sep %3
  }
  else
  {
    ; default is a pipe.
    set %sep |
  }


  str Count %1 %sep
  set %sep_count #strRes

  set %first_sep_index %2
  set %second_sep_index 1 + %2

  str Pos %1 %sep %first_sep_index
  set %first_sep_pos #strRes

  str Pos %1 %sep %second_sep_index
  set %second_sep_pos #strRes

  ; getting a space char after the desired string. removing with -1
  set %index_string_length %second_sep_pos - %first_sep_pos - 1

  ; for some reason, I am getting a space char here. Need to +1 to remove.
  set %first_sep_pos %first_sep_pos + 1
  ; Get the substring between separators
  str Mid %1 %first_sep_pos %index_string_length
  set %substring #strRes

  ; Unsetting the sep variable so it does not default later.
  deletevar %3

  return %substring
}

; %1: Container to bank from
sub bankOre
  set %containerToBankFrom %1

  event sysMessage Banking ore

  ; Open the container just in case
  gosub useObject %containerToBankFrom

  ; attempt to open the character's bank box
  event Macro 1 0 banco

  ; for each ore type
  for %ore_type_index 1 %ore_pile_graphics_len
  {
    ; Get the ore graphic id
    gosub getIndex %ore_pile_graphics %ore_type_index |
    set %current_ore_type #result

    findItem %current_ore_type %containerToBankFrom

    for #FINDINDEX 1 #FINDCNT
    {
      if #FINDBAGID = %containerToBankFrom
      {
        gosub moveItemToContainer #FINDID %bank_serial
      }
    }
  }

  ; if we are anywhere near overweight still,
  if ( #WEIGHT + 50 >= #MAXWEIGHT )
  {
    ; then lets stop the script with an exception message
    event sysMessage Character does not have enough free space to mine. Please bank some stuff.
    stop
  }
  else
  {
    event sysMessage Finished banking ore
  }
return

sub restockMiningResources
  ; In order for us to mine, we really need to have at least one
  ; tinker's kit and 20 or so iron ingots.
  ; If the player does not have them on hand, will check the main
  ; bank container for these items.
  ; We are going to asusme that the bankbox is already open.

  ; Double-checking for tinker toolkits. If we do not have one, stop.
  finditem KTL %backpack

  if ( #FINDCNT = 0 )
  {
    event sysMessage We did not have tinker's tools. Please restock and restart.
    stop
  }

  ; Double-checking for ingots. We need at least 10.
  finditem ENK %backpack

  if ( #FINDCNT = 0 || #FINDSTACK < 50 )
  {
    event sysMessage Did not find enough ingots, attempting restock.

    finditem ENK C_ , %bank_serial

    ; If the player does not even have enough in the bank,
    if ( #FINDCNT = 0 || #FINDSTACK < 50 )
    {
      event sysMessage Did not find enough ingots in the bank, stopping.
      stop
    }
    else
    {
      event sysMessage Restocking ingots
      ; move it to the bank
      ; TODO: only grab enough to get us to 50.
      exevent drag #FINDID 50
      wait 1s
      exevent dropc #BACKPACKID
      wait 1s
    }
  }
return

; %1: Item Type
; %2: Container
; %3: Color
sub findFirstItemByColor
  set %firstItem N/A

  finditem %1 %2

  ; If we found something,
  if ( #FINDCNT > 0 )
  {
    ; for each item that we found,
    for #FINDINDEX 1 #FINDCNT
    {
      ; If the found item has the right color,
      if #FINDCOL = %3 && #FINDBAGID = #BACKPACKID
      {
        set %firstItem #FINDID
      }
    }
  }

return %firstItem

sub combineOre
  ; for each ore color,
  for %ore_color_index 1 %ore_colors_len
  {
    gosub getIndex %ore_colors %ore_color_index |
    set %ore_color #RESULT

    event sysMessage Combing ore for color: %ore_color

    ; First, we need to get a small of the ore color
    gosub findFirstItemByColor TVJ %backpack %ore_color
    set %small_ore_pile_id #RESULT

    event sysMessage small id: %small_ore_pile_id

    if %small_ore_pile_id <> N/A
    {
      for %large_pile_type 2 %ore_pile_graphics_len
      {
        gosub getIndex %ore_pile_graphics %large_pile_type |
        set %ore_graphic #RESULT

        event sysMessage Searching for ore graphic: %ore_graphic

        ; There should only be one item of the type for each color,
        ; so we do not really need to for-loop here.
        gosub findFirstItemByColor %ore_graphic %backpack %ore_color
        set %bigOrePile #RESULT

        if %bigOrePile <> N/A
        {
          gosub useObject %bigOrePile

          ; wait for target
          target 5s

          ; target the little pile
          gosub targetObject %small_ore_pile_id
          wait 1s
        }
      }
    }
  }
return

set %no_metal no_metal_here_to_mine.
set %cannot_mind can't_mine_there.
set %cannot_see cannot_be_seen.

sub miningStopMessageReceived
  scanJournal #JIndex
  set %received #FALSE

  if ( %no_metal in #JOURNAL )
  {
    set %received #TRUE
  }
  if ( %cannot_mind in #JOURNAL )
  {
    set %received #TRUE
  }
  if ( %cannot_see in #JOURNAL )
  {
    set %received #TRUE
  }

  set #RESULT %received
return %received

; %1: X Coordinate
; %2: Y Coordinate
; %3: Z Coordinate
sub mineXYZ
  ; Perform steps to mine a location, such as
  ; searching for tools, making them, and use
  ; them to target a location. Also cleaning up
  ; our backpack if we run out of space.
  event sysMessage Mining location: %1 %2 %3

  set %mining_location_x %1
  set %mining_location_y %2
  set %mining_location_z %3

  set %finishedMining #FALSE

  while %finishedMining <> #TRUE
  {
    ; Before we even try to mine, we need to ensure that
    ; we have enough space in our backpack to mine.
    if #WEIGHT + 20 > #MAXWEIGHT
    {
      gosub combineOre
    }

    event sysMessage Attempting to find or make pickaxe

    gosub findOrMakePickaxe
    set %pickaxeId #RESULT

    event sysMessage Using found pickaxe
    gosub useObject %pickaxeId

    ; wait for target
    target 5s

    ; set last ground target and target ground
    gosub targetGroundXYZ %mining_location_x %mining_location_y %mining_location_z

    wait 1s

    gosub miningStopMessageReceived
    set %finishedMining #RESULT
  }
return

; %1: X Coordinate
; %2: Y Coordinate
; %3: Radius from coordinate
sub walkToXY
  ; Using the built-in dumb move. There's a pathfinder version,
  ; but that feels overkill and also has the drawback of not
  ; stopping the script's execution while it's running. All I
  ; have to do is stay cognizant of the limitations of move
  ; and this should work just fine.

  event sysMessage Walking to point.
  move %1 %2 %3 5s
return

sub findOrMakePickaxe

  set %found_pickaxe #FALSE

  while ( ! %found_pickaxe )
  {
    ; Try to find a pickaxe to use
    finditem QPF %backpack

    if ( #FINDCNT < 1 )
    {
      ; Double-check we have enough ingots on hand,
      finditem ENK C_ , #BACKPACKID
      set %ingot_count #FINDSTACK

      ; Try to find a toolkit
      finditem KTL %backpack
      set %toolkit_id #FINDID

      if ( #FINDCNT < 1 || %ingot_count < 4 )
      {
        event sysMessage Did not find tools or enough ingots, stopping!
        stop
      }

      ; Double-click on tools by setting last target, use last target
      gosub useObject %toolkit_id

      ; Wait for the gump to appear. This is rather hard to do an actual
      ; wait, so we are just going to use seconds here. This does not
      ; happen THAT often, so it's not a huge cost to eat.
      wait 1s

      ; First, we click on the Tools category
      gosub clickGumpXY 25 110

      if ( #FINDCNT < 2 )
      {
        event sysMessage Only found one toolkit, making another.
        ; Click on tinker's toolkit button
        gosub clickGumpXY 230 128
      }
      else
      {
        event sysMessage Making a pickaxe!
        ; Click Next Page button
        gosub clickGumpXY 380 270

        wait 1s

        ; Click pickaxe button
        gosub clickGumpXY 234 191
      }

      ; The server does add a pause to the item creation here.
      ; Must wait to register that the toolkit or pickaxe are
      ; in our backpack after the creation attempt.
      wait 2s
    }
    else
    {
      set %pickaxeId #FINDID
      set %found_pickaxe #TRUE
    }
  }

return %pickaxeId

; %1: Object ID
; %2: Container ID
sub moveItemToContainer
    ; move it to the bank
    exevent drag %1 60000
    wait 1s
    exevent dropc %2
    wait 1s
return

sub moveOreToPackAnimal
  for %graphic_index 1 %ore_pile_graphics_len
  {
    gosub getIndex %ore_pile_graphics %graphic_index |
    set %ore_graphic #RESULT

    event sysMessage Searching for ore graphic: %ore_graphic

    finditem %ore_graphic %backpackid

    event sysMessage Num found: #FINDCNT

    ; For each of the piles we found,
    for #FINDINDEX 1 #FINDCNT
    {
      event sysMessage Index: #FINDINDEX

      if #FINDBAGID = #BACKPACKID
      {
        event sysMessage Moving ore pile with id: #FINDID
        gosub moveItemToContainer #FINDID %packanimal_id
      }
    }
  }
return

sub automine
  ; This is the main mining loop
  event sysMessage Starting to mine!

  ; This is where any initial information gathering should take place.
  gosub checkForPackAnimals

  ; while not dead,
  while #charGhost = NO
  {
    ; this is here because we can attempt to come back around,
    ; get lost, and then not be at the starting position.
    ; Also, a bit more dire. We could have been at the bank,
    ; gotten jailed, and now are no longer where we should be.
    gosub ensureAtStartingPosition

    gosub bankOre #BACKPACKID
    gosub bankOre %packanimal_id_1
    gosub bankOre %packanimal_id_2
    gosub bankOre %packanimal_id_3
    gosub bankOre %packanimal_id_4
    gosub bankOre %packanimal_id_5

    gosub restockMiningResources

    ; TODO add something here for feeding pets if we have them

    ; this is different from location_index in that those also
    ; include waypoints. This is necessary for picking the pack
    ; horse to give ore to.
    set %current_mining_spot_index 1

    ; for each location,
    for %location_index 1 %locations_len
    {
      ; Pull out the info for the next location
      gosub getIndex %locations %location_index |
      set %current_location_values #result

      gosub getIndex %current_location_values 1 _
      set %current_location_x #RESULT

      gosub getIndex %current_location_values 2 _
      set %current_location_y #RESULT

      gosub getIndex %current_location_values 3 _
      set %current_location_z #RESULT

      event sysMessage Walking to: %current_location_x %current_location_y

      gosub walkToXY %current_location_x %current_location_y 2


      if %current_location_z <> W
      {
        gosub mineXYZ %current_location_x %current_location_y %current_location_z

        ; Once we're done mining, we need to do a combine ore at the end
        gosub combineOre

        ; Because we are going to have to juggle a number of horses, we need
        ; to figure out a way to give each of them ore so that no one horse
        ; gets overweight otherwise we're in trouble.
        ; What I have chosen to do is state that a given horse has six different
        ; mining spots that they get ore at. Once we move beyond those spots,
        ; then it goes to a different horse. That way it is fairly balanced.
        if %current_mining_spot_index < 7
        {
          set %packanimal_id %packanimal_id_1
        }
        else
        {
          if %current_mining_spot_index < 13
          {
            set %packanimal_id %packanimal_id_2
          }
          else
          {
            if %current_mining_spot_index < 19
            {
              set %packanimal_id %packanimal_id_3
            }
            else
            {
              if %current_mining_spot_index < 25
              {
                set %packanimal_id %packanimal_id_4
              }
              else
              {
                set %packanimal_id %packanimal_id_5
              }
            }
          }
        }

        ; After that, we can stow onto a pack animal.
        ; Pack horse object type: ZJF
        gosub moveOreToPackAnimal %packanimal_id

        ; Increment our spot tracker for mining spots
        set %current_mining_spot_index %current_mining_spot_index + 1
      }
    }
  }
return

event sysMessage Finished loading in all of the code.

gosub automine
;gosub combineOre
stop
